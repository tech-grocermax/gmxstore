buildscript {
   repositories {
       maven {
         url 'http://artifactory.engr.wavemarket.com/releases'
       }
   }

   dependencies {
      classpath(group: 'com.locationlabs', name: 'gradle_ubuntu_packager', version: '+')
   }
}

def lines = new File("gmxstore/setup.py").readLines()
def pattern = ~/^__version__\s*=\s*['"]([^\s]+)["']/
def version = lines*.find(pattern, {match, version -> return version}).find()

if (!version) {
    throw new ResourceException("Unable to parse __version__ from gmxstore/setup.py")
}

project.version = version
project.description = "Grocermax Android app store(Grocermax Play Store)"

project.ext.dists = ["gmxstore", "gmxstoredeploy"]

apply from: "${System.getenv().GRADLE_COMMON}/plugins/python/python.gradle"

virtualEnv.makeRelocatable()

apply plugin: 'ubuntu'

ubuntu {
   name         = "gmxstore"
   author       = "Location Labs"
   debDir       = "gmxstore/dpkg"
   email        = "info@locationlabs.com"
   homepage     = "http://locationlabs.com"
   releaseNotes = "Initial version of the gmxstore project."
   depends {
      on "nginx"
      // depend on the specific python version because
      // paths inside the virtualenv include the version number;
      // a different version of python will not look in those directories
      on "python" + getPythonVersion()
      on "python-setuptools"
      on "supervisor"
      on "build-essential"
   }
}

deb {
   dependsOn createDist

   doFirst {
      ubuntu {
         // gmxstore virtualenv
         addFilesToDeb(virtualEnv_gmxstore.virtual_env.listFiles(),
                       "/usr/lib/gmxstore/venv")

         // gmxstore's own configuration
         addFileToDeb(new File("gmxstore/conf/etc/gmxstore/gmxstore.conf"),
                      "/etc/gmxstore")

         // service configurations
         addFileToDeb(new File("gmxstore/conf/etc/nginx/sites-available/gmxstore"),
                      "/etc/nginx/sites-available")
         addFileToDeb(new File("gmxstore/conf/etc/gmxstore/uwsgi.ini"),
                      "/etc/gmxstore")
         addFileToDeb(new File("gmxstore/conf/etc/supervisor/conf.d/gmxstore.conf"),
                      "/etc/supervisor/conf.d")
         addFileToDeb(new File("gmxstore/conf/etc/logrotate.d/gmxstore"),
                      "/etc/logrotate.d")

         // documentation
         addFileToDeb(new File("gmxstore/doc/README"),
                      "/usr/share/doc/gmxstore")
      }

      dirs {
         dir "/var/log/gmxstore"
      }
   }
}
